Algoritmo tiempo
	//ENUNCIADO:Un cuerpo posee una velocidad inicial de 12 m/s y una aceleración de 2 m/s2 ¿Cuánto tiempo tardará en adquirir una velocidad de 144 Km/h?
	//CREADO POR: HERNAN ALBERTO LONDOÑO VELEZ
	//VERSION=1.0
	//FECHA=22/02/2023
	
	//DEFINIR:
	Definir v_vi Como Real //VARAIABLE QUE ALAMACENA EL VALOR DE VELOCIDAD INICIAL
	Definir v_vf Como Real //VARAIBLE QUE ALAMACENA EL VALOR DE VELOCIDAD FINAL
	Definir v_a Como Real //VARIABLE QUE ALAMCENA EL VALOR DE ACELERACION
	Definir v_mvi Como Entero //VARIABLE QUE ALMACENA LA MEDICION DE VELOCIDAD DE VELOCIDAD INICIAL
	Definir v_mvf Como Entero //VARIABLE QUE ALMACENA LA MEDICION DE VELOCIDAD DE VELOCIDAD FINAL
	Definir v_ma Como Entero //VARIABLE QUE ALAMCENA LA MEDICION DE ACELERACION
	Definir v_vi2 Como Real //VARIABLE QUE ALMACENA EL VALOR DE VELOCIDAD INICIAL EN M/S
	Definir v_vf2 Como Real //VARIABLE QUE ALMACENA EL VALOR DE VELOCIDAD FINAL EN M/S
	Definir v_a2 Como Real //VARIABLE QUE ALMACENA EL VALOR DE ACELERACION EN M/S2
	Definir vf_vi Como Real //VARIABLE QUE ALMACENA EL RESULTADO DE UNA OPERACION ARITMETICA DE VF-VI
	Definir v_t Como Real //VARIABLE QUE ALMACENA EL RESULTADO DE UNA OPERACION ARITMETICA, DE TIEMPO
	
	//VALOR INICIAL:
	v_vi=0.0
	v_vf=0.0
	v_a=0.0
	v_mvi=0
	v_mvf=0
	v_ma=0
	v_vi2=0.0
	v_vf2=0.0
	v_a2=0.0
	vf_vi=0.0
	v_t=0.0
	
	//INICIO
	//ENTRADA DE DATOS
	Escribir "escribe velocidad inicial"
	leer v_vi
	//ENTRADA DE DATOS
	escribir " si velocidad inicial esta en m/s escriba 1 ó si esta en km/h escriba 2"
	leer v_mvi
	//ENTRADA DE DATOS
	escribir "escribe velocidad final"
	leer v_vf
	//ENTRADA DE DATOS
	escribir "si velocidad final esta en m/s escriba 1 ó si esta en km/h escriba 2"
	leer v_mvf
	//ENTRADA DE DATOS
	escribir "escribe aceleración"
	leer v_a
	//ENTRADA DE DATOS
	escribir "si aceleracion esta en m/s2 escriba 1 ó si esta en km/h2 escriba 2"
	leer v_ma
	
	escribir "-------------------------------------------------"
	//PROCESO
	Segun v_mvi Hacer //SE CONVIERTE KM/H A M/S DE VELOCIDAD INICIAL
		1:
			v_vi2 =v_vi
		2:
			v_vi2 =v_vi/3.6
	Fin Segun
	//PROCESO
	Escribir "velocidad inicial en m/s es:",v_vi2 "m/s"
	//PROCESO
	Segun v_mvf Hacer //SE CONVIERTE KM/H A M/S DE VELOCIDAD FINAL
		1:
			v_vf2 =v_vf
		2:
			v_vf2 =v_vf/3.6
	Fin Segun
	//PROCESO
	Escribir "velocidad final en m/s es:",v_vf2 "m/s"
	//PROCESO
	Segun v_ma Hacer //SE CONVIERTE KM/H AL CUADRADO EN M/S AL CUADRADO  DE ACELERACION
		1:
			v_a2 =v_a
		2:
			v_a2 =v_a/12960
	Fin Segun
	//PROCESO
	escribir "aceleració en m/s2 es:",v_a2 "m/s2"
	Escribir "-------------------------------------------------"
	Escribir "los resultados son:"
	//SALIDA
	vf_vi =v_vf2-v_vi2 //PARA OBTERNER LA VARIANLE VF_VI, SE RESTA VELOCIDAD FINAL CON VELOCIDAD INICIAL
	v_t =vf_vi/v_a2 //PARA OBTENER TIEMPO, SE DIVIDE VF_VI ENTRE ACELERACION
	//SALIDA
	Escribir "el tiempo es:",v_t "seg"
	//FIN
FinAlgoritmo


