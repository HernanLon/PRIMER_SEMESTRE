Algoritmo sin_titulo
	//ENUNCIADO=
	//Se desea obtener la nómina semanal ?salario neto? de los empleados de una empresa cuyo trabajo se paga por 
    //horas y del modo siguiente:
	//las horas inferiores o iguales a 35 horas (normales) se pagan a una tarifa determinada que se debe introducir 
	//por teclado al igual que el número de horas y el nombre del trabajador,
	//las horas superiores a 35 se pagarán como extras a un promedio de 1,5 horas normales,
    //los impuestos a deducir a los trabajadores varían en función de su sueldo mensual:
	//sueldo <= 2.000, libre de impuestos,
	//las siguientes 220 euros al 20 por 100,
	//el resto, al 30 por 100.
	
	//CREADO POR= Hernan Alberto Londoño Velez
	//FECHA: 03/03/2023
	//VERSION: 1.0
	
	
	//DEFINICION=
	Definir v_nombre Como Caracter//VARIABLE QUE ALMACENA EL NOMBRE DEL TRABAJADOR
	Definir v_horas Como Entero//VARIABLE QUE ALMACENA EL NUMERO DE HORAS
	Definir v_impuestos Como Real//VARIABLE QUE ALMACENA EL VALOR DE LOS IMPUESTOS
	Definir v_sbruto Como Real//VARIABLE QUE ALMACENA EL VALOS DE SALARIO BRUTO
	Definir v_sneto Como Real//VARIABLE QUE ALMACENA EL VALOS DE SALARIO NETO
	Definir v_tarifa Como Entero//VARIABLE QUE ALMACENA EL VALOR DE LA HORA DE TRABAJO
	
	//VALOR INICIAL:
	v_nombre=""
	v_horas=0
	v_impuestos=0.0
	v_sbruto=0.0
	v_sneto=0.0
	v_tarifa=0	
	
	//INICIO
	Escribir "escriba el nombre del empleado"
	leer v_nombre
	
	Escribir "escriba la cantidad de horas trabajadas"
	Leer v_horas
	
	Escribir "escribor el valor de la hora de trabajo"
	Leer v_tarifa
	
	//PROCESO
	Si v_horas<=35 Entonces //SE DECIDE SI EL NUMERO DE HORAS ES MENOR O IGUAL A 35
		v_sbruto=v_horas*v_tarifa //SI ES MENOR O IGUAL A 35: LA CANTIDAD DE HORAS SE MULTIPLICA POR EL VALOR DE LA TARIFA PARA OBTENER EL SALARIO BRUTO
	SiNo
		v_sbruto=35*v_tarifa+((v_horas-35)*1.5*v_tarifa) //SI ES MAYOR A 35: ((SE RESTA LA CANTIDAD DE HORAS CON 35) SE MULTIPLICA POR 1.5 Y POR EL VALOR DE LA TARIFA) Y SE SUMA COMO (35 MULTIPLICADO POR EL VALOR DE LA TARIFA)
	Fin Si
	
	Si v_sbruto<=2000 Entonces //SE DECIDE SI EL SALARIO BRUTO ES MENOR O IGUAL A 2000
		v_impuestos=0 //SI SALARIO BRUTO ES MENOR O IGUAL A 2000: NO HAY IMPUESTOS
	SiNo //SI SALARIO BRUTO ES MAYOR A 2000 ENTONCES:
		Si (v_sbruto>2000) y (v_sbruto<=2220) Entonces  //SE DECIDE SI SALARIO BRUTO ES MAYOR A 2000 Y MENOR O IGUAL A 2220
			v_impuestos=(v_sbruto-2000)*0.2 //SI LA CONDICION SE CUMPLE ENTONCES: PARA OBTENER EL VALOR LOS IMPUESTOS SE (RESTA EL SALARIO BRUTO CON 2000) Y SE MULTIPLICA POR 0.2 (20% = 0.2)
		SiNo //SI LA CONDICION NO SE CUMPLE ENTONCES:
			v_impuestos=(220*0.2)+((v_sbruto-2220)*0.3) //PARA OBTENER EL VALOR DE LOS IMPUESTOS: ((SE RESTA EL SALARIO BRUTO CON 2220)SE MULTIPLICA POR 0.3) Y SE SUMA CON EL RESULADO DE (220 MULTIPLICADO POR 0.2)
		Fin Si
	Fin Si
	
	v_sneto=v_sbruto-v_impuestos //PARA OBTENER EL VALOR DEL SALARIO NETO: SE RESTA EL SALARIO BRUTO CON LOS IMPUESTOS
	
	//SALIDA
	Escribir "Nombre reabajador="; Escribir v_nombre
	Escribir "El salario bruto es="; Escribir v_sbruto
	Escribir "El valor de los impuestos es="; Escribir v_impuestos
	Escribir "El salario neto es="; Escribir v_sneto
	//FIN
FinAlgoritmo
