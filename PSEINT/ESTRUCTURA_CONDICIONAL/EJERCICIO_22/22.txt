Algoritmo SOLUCION_ECUACION
	//ENUNCIADO=Pseudocódigo que nos permita calcular las soluciones de una ecuación de segundo grado, incluyendo los valores imaginarios
	
	//Definir 
	Definir v_a Como Real//VARIABLE QUE ALMACENA EL VALOR INTRODUCIDO DE A
	Definir v_b Como Real//VARIABLE QUE ALMACENA EL VALOR INTRODUCIDO DE B
	Definir v_c Como Real//VARIABLE QUE ALMACENA EL VALOR INTRODUCIDO DE C
	Definir v_r Como Real//VARIABLE QUE ALMACENA EL RESULTADO DE LA OPERACION R
	Definir v_i Como Real//VARIABLE QUE ALMACENA EL RESULTADO DE LA OPERACION I
	Definir v_x1 Como Real//VARIABLE QUE ALMACENA EL RESULTADO DE LA OPERACION X1, PRIMER CORTE CON EL EJER X
	Definir v_x2 Como Real//VARIABLE QUE ALMACENA EL RESULTADO DE LA OPERACION X2, SEGUNDO CORTE CON EL EJE X
	
	//VALOR INICIAL:
	v_a=0.0
	v_b=0.0
	v_c=0.0
	v_r=0.0
	v_i=0.0
	v_x1=0.0
	v_x2=0.0
	
	//INTRODUCCION DE DATOS
	Escribir "escribe el coeficiente a"
	Leer v_a
	
	Escribir "escribe el coeficiente b"
	Leer v_b
	
	Escribir "escribe el coeficiente c"
	Leer v_c
	
	//PROCESO Y SALIDA
	Si v_a<>0 Entonces //SE DECIDE SI EL VALOR DE A ES DIFERENTE DE CERO
		v_d=(v_b*v_b)-4*v_a*v_c //PARA OBTENER LA V_D ENTONCES: (EL VALOR B SE MULTIPLICA POR EL VALOR B) SE RESTA CON 4 Y SE MULTIPLICA POR EL VALOR A Y EL VALOR C
		Si v_d<>0 Entonces //SE DECIDE SI V_D ES DIFERENTE DE CERO
			Si v_d>0 Entonces //SE DECIDE SI V_D ES MAYOR A CERO
				v_x1=(-v_b+raiz(v_d))/(2*v_a) //PARA OBTENER EL PRIMER CORTE DE X : ((SE LE SACA RAIZ CUADRADA A V_D) SE LE SUMA EL VALOR DE B CON EL SIGNO CONTARIO) SE DIVIDE ENTRE (2 MULTIPLICADO POR EL VALOR DE A)
				v_x2=(-v_b-raiz(v_d))/(2*v_a) //PARA OBTENER EL SEGUNDO CORTE DE X : ((SE LE SACA RAIZ CUADRADA A V_D) SE LE RESTA EL VALOR DE B CON EL SIGNO CONTARIO) SE DIVIDE ENTRE (2 MULTIPLICADO POR EL VALOR DE A)
				Escribir "x1=",v_x1
				Escribir "x2=",v_x2
			SiNo //SI EL VALOR DE V_D ES MENOR A CERO ENTONCES:
				v_r=(-v_b)/(2*v_a) //PARA OBTENER V_R: (EL VALOR DE B CON SIGNO CONTRARIO) DIVIDIDO ENTRE (2 MULTIPLICADO POR VALOR DE A)
				v_i=raiz(abs(v_d))/(2*V_a) //PARA OBTENER V_I: (VALOR ABSOLUTO DEL VALOR B) DIVIDIDO ENTRE (2 MULTIPLICADO POR VALOR DE A) SE LE SACA RAIZ CUADRADA
				Escribir v_r,"+",v_i,"i"
				Escribir v_r,"-",v_i,"i"
			Fin Si
		SiNo //SI EL VALOR DE V_D ES IGUAL A CERO ENTONCES:
			v_x1=-v_b/(2*v_a) //PARA OBTENER EL PRIMER CORTE X: (VALOR DE B CON EL SIGNO CONTRARIO) DIVIDIDO ENTRE (2 MULTIPLICADO POR VALOR DE A)
			v_x2=v_x1 //EL SEGUNDO CORTE ES IGUAL AL PRIMER CORTE
			Escribir "x1=",v_x1
			Escribir "x2=",v_x2
		Fin Si
	SiNo
		Escribir "no es una ecuacion de segundo grado" //SI EL VALOR DE A ES IGUAL A CERO ENTONCES SE COMUNICA QUE NO ES UNA ECUACION DE SEGUNDO GRADO
	Fin Si
FinAlgoritmo
